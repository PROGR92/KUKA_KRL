&ACCESS RVO1
&REL 2
&COMMENT Przejazd do HOME_POS
&PARAM DISKPATH = KRC:\R1\Program\dodatkowe
DEF to_home_R1( )
;FOLD DECLARATIONS
;DLA RESTARTU
DECL E6AXIS XActualAxis, XAboveHomePos, XNearHomePos
DECL E6POS XActualPosition, XGO_UP
DECL LDAT LGO_UP
DECL PDAT PACT_POS 
;ENDFOLD (DECLARATIONS)
;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here
  ;DO RUCHU AUTOMATYCZNEGO DO POZYCJI $HOME
   LGO_UP = {VEL 0.5,ACC 50.0,APO_DIST 50.0,APO_FAC 50.0,ORI_TYP #VAR} 
   PACT_POS = {VEL 5.0,ACC 100.0,APO_DIST 1.0}
   ;ENDFOLD (USER INI)
  
;ENDFOLD (INI)


IF $IN_HOME==FALSE THEN
;FOLD PRAMETRY STARTOWE

;INICJALIZACJA UKLADOW ODNIESIENIA
$BASE = $nullframe
$TOOL = TOOL_DATA[1]
;Pobranie aktualnej pozycji ROBOTA osie / przestrzenna
XActualAxis = $AXIS_ACT
XActualPosition = $POS_ACT

;ENDFOLD
;FOLD PTP ActualPosition  Vel=5 % ACT_POS Tool[1]:Flansch Base[0];%{PE}%R 5.4.33,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:ActualPosition, 3:, 5:5, 7:ACT_POS
$BWDSTART=FALSE
PDAT_ACT=PACT_POS
FDAT_ACT=FDEFAULT
BAS(#PTP_PARAMS,5)
PTP XActualPosition 
;ENDFOLD

XGO_UP = XActualPosition

;FOLD KONTROLA POZYCJI ROBOTA W PRZESTRZENI ROBOCZEJ

XGO_UP = XActualPosition

IF XActualAxis.A1 < 57 THEN ;ROBOT NAD STREFA PALET Z WELNA

    XGO_UP.Z = 1200.0 ;POZYCJA -Z- zblizona do wysokosci przejazdowej nad stosami 
ENDIF ;XActualAxis.A1 < 57 

IF XActualAxis.A1 >= 57 THEN     ;ROBOT NAD MIEJSCEM ODKLADCZYM PALET ROBOTA R_1

   IF XActualAxis.A1 > 135 THEN ; NAD PALETA  

         XGO_UP.Z = 400.0 ;POZYCJA -Z- zblizona do wysokosci przejazdowej nad stosami  
   ELSE

         WAIT FOR FALSE       
   ENDIF
ENDIF ;XActualAxis.A1 <= -47  

;ENDFOLD (KONTROLA POZYCJI ROBOTA W PRZESTRZENI ROBOCZEJ)

;Ruch do bezpiecznej wysokosci
;FOLD LIN GO_UP  Vel=0.5 m/s DEFAULT Tool[1]:Flansch Base[0];%{PE}%R 5.4.33,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:GO_UP, 3:, 5:0.5, 7:DEFAULT
$BWDSTART=FALSE
LDAT_ACT=LGO_UP
FDAT_ACT=FDEFAULT
BAS(#CP_PARAMS,0.5)
LIN XGO_UP 
;ENDFOLD 

XAboveHomePos = $AXIS_ACT
XAboveHomePos.A1=57
;FOLD PTP AboveHomePos Vel=100 % PDEFAULT Tool[2]:TCP_PALETA Base[0];%{PE}
;FOLD Parameters ;%{h}
;Params IlfProvider=kukaroboter.basistech.inlineforms.movement.old; Kuka.IsGlobalPoint=False; Kuka.PointName=AboveHomePos; Kuka.BlendingEnabled=False; Kuka.MoveDataPtpName=PDEFAULT; Kuka.VelocityPtp=100; Kuka.CurrentCDSetIndex=0; Kuka.MovementParameterFieldEnabled=True; IlfCommand=PTP
;ENDFOLD
$BWDSTART = FALSE
PDAT_ACT = PDEFAULT
FDAT_ACT = FDEFAULT
BAS(#PTP_PARAMS, 10.0)
SET_CD_PARAMS (0)
PTP XAboveHomePos
;ENDFOLD
 
XNearHomePos = XHome
XNearHomePos.A6=XAboveHomePos.A6
;FOLD PTP NearHomePos  Vel=10 % DEFAULT Tool[0] Base[0];%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:NearHomePos, 3:, 5:10, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FDEFAULT
BAS(#PTP_PARAMS,10)
PTP XNearHomePos 
;ENDFOLD

REPEAT 
UNTIL ( NOT $IN[10])

;OCZEKIWANIE NA PUSTY CHWYTAK
;FOLD WAIT FOR ( NOT IN 144 'Krancowka boczna' );%{PE}%R 8.5.19,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:NOT, 5:$IN, 6:144, 7:Krancowka boczna, 9:
WAIT FOR  (  NOT $IN[144] ) 
;ENDFOLD

;FOLD PTP HOME Vel=30 % DEFAULT;%{PE}%R 5.5.32,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:30, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,30)
$H_POS=XHOME
PTP XHOME 
;ENDFOLD

ENDIF 

END
DEF DROP_ALL() 

BRAKE 
WAIT SEC 0.5
;FOLD OUT 6 'ZASSANIE_PRZYSS_1' State=FALSE ;%{PE}%R 5.5.32,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:6, 3:ZASSANIE_PRZYSS_1, 5:FALSE, 6:
$OUT[6]=FALSE
;ENDFOLD
;FOLD OUT 7 'ZASSANIE_PRZYSS_2' State=FALSE ;%{PE}%R 5.5.32,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:7, 3:ZASSANIE_PRZYSS_2, 5:FALSE, 6:
$OUT[7]=FALSE
;ENDFOLD
;FOLD OUT 8 'WYDMUCH_PRZYSSAWEK' State=TRUE ;%{PE}%R 5.5.32,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:8, 3:WYDMUCH_PRZYSSAWEK, 5:TRUE, 6:
$OUT[8]=TRUE
;ENDFOLD
;FOLD WAIT Time=1 sec;%{PE}%R 5.5.32,%MKUKATPBASIS,%CWAIT,%VWAIT,%P 2:1
WAIT SEC 1
;ENDFOLD
;FOLD OUT 8 'WYDMUCH_PRZYSSAWEK' State=FALSE ;%{PE}%R 5.5.32,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:8, 3:WYDMUCH_PRZYSSAWEK, 5:FALSE, 6:
$OUT[8]=FALSE
;ENDFOLD

INTERRUPT OFF 128 ;Wymuszenie zrzutu elementow
HALT
WAIT SEC 0.5

END